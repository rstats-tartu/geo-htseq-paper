
rule all:
    input:
        "results/pvalues.csv",
        "results/sequencing_metadata_unique_platform.csv",
        expand("figures/figure_{n}.tiff", n = [1,2,3,4])


rule download:
    output:
        "results/geo-htseq-until-2019-12-31.tar.gz",
    log:
        "results/log/download.log",
    resources:
        runtime=120,
        mem_mb=4000,
    shell:
        "curl -o {output} https://zenodo.org/record/4046422/files/geo-htseq-until-2019-12-31.tar.gz?download=1 2> {log}"


rule unpack:
    input:
        rules.download.output[0],
    output:
        "results/data/document_summaries.csv",
        "results/data/publications.csv",
        "results/data/single-cell.csv",
        "results/data/suppfilenames.txt",
        "results/data/parsed_suppfiles.csv",
        "results/data/scopus_citedbycount.csv",
        "results/data/spots.csv",
        "results/data/suppfilenames_filtered.txt",
    log:
        "results/log/unpack.log",
    shadow:
        "minimal"
    resources:
        runtime=120,
        mem_mb=4000,
    shell:
        "(tar -xzvf {input} "
        " && mkdir -p $(dirname {output[0]})"
        " && cp output/* $(dirname {output[0]})) 2> {log}"


rule preprocess_pvalues:
    input:
        "resources/data/document_summaries.csv",
        "resources/data/suppfilenames.txt",
        "resources/data/parsed_suppfiles.csv",
    output:
        "results/conformity_acc.csv",
        "results/pvalues.csv",
        "results/pvalues_sample.csv",
        "results/pvalues_acc.csv",
    log:
        "results/log/preprocess_pvalues.log",
    conda:
        "envs/environment.yml"
    resources:
        runtime=120,
        mem_mb=4000,
    script:
        "scripts/preprocess_pvalues.R"


rule preprocess_sequence_metadata:
    input:
        "results/data/spots.csv",
    output:
        "results/sequencing_metadata_unique_platform.csv",
    log:
        "results/log/preprocess_sequence_metadata.log",
    conda:
        "envs/environment.yml"
    resources:
        runtime=120,
        mem_mb=4000,
    script:
        "scripts/preprocess_sequence_metadata.R"


rule figures:
    input:
        "results/conformity_acc.csv",
        "results/pvalues_sample.csv",
        "results/sequencing_metadata_unique_platform.csv",
        "results/data/parsed_suppfiles.csv",
        "results/pvalues_acc.csv",
    output:
        "results/models/conforms_year.rds",
        "figures/figure_1.tiff",
        "results/files_to_check_pi0.txt",
        "results/models/Class_1.rds",
        "figures/figure_2.tiff",
        "results/models/Class_year__year_detool_year_muunif2.8k.rds",
        "results/models/Class_detool_2018-19_priors_muunif.rds",
        "figures/figure_3.tiff",
        "results/models/pi0_detool.rds",
        "results/models/pi0_year__year_detool.rds",
        "figures/figure_4.tiff",
    log:
        "results/log/figures.log",
    container:
        "file:geo-htseq.simg"
    resources:
        runtime=1440,
        mem_mb=8000,
    script:
        "scripts/figures.R"
